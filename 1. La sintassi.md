# CAPITOLO 1 #
## La sintassi in CoffeeScript ##

In primo luogo , prima di entrare nel vivo di questa sezione, voglio ribadire che nonostante spesso la sintassi di CoffeeScript sia identico a quella di JavaScript, CoffeeScript non è un superset e quindi alcune keyword JavaScript, come ad esempio `function` e `var`, non sono consentiti e questo solleva degli errori di sintassi. Se stai scrivendo un file CoffeeScript, bisogna che sia puro CoffeeScript, non possono mescolarsi i due linguaggi.

Perché non è CoffeeScript un superset? Beh, il fatto stesso che lo spazio bianco è significativo nei programmi CoffeeScript gli impedisce di essere un superset. E', una volta che la decisione è stata presa, il team ha deciso anche di deprecare alcune keyword e caratteristiche di JavaScript in nome della semplicità e nello sforzo di ridurre molti
dei bug che si verificano comunemente.

Quello che trovo strabiliante, in una sorta di meta modo, è che l'interprete CoffeeScript
sia effettivamente scritto in CoffeeScript. Sembra che il paradosso dell'uovo e della gallina sia finalmente stato risulto!

Giusto, quindi in primo luogo cerchiamo di affrontare le cose di base. Non ci sono punti e virgola in CoffeeScript, vengono aggiunti automaticamente al momento della compilazione. Il punto è virgola è stato causa di gran parte dibattito nella comunità JavaScript, e sta alla base di alcuni [comportamenti strani](http://bonsaiden.github.com/JavaScript-Garden/#core.semicolon "Comportamenti strani") dell'interprete. Comunque, CoffeeScript risolve questo problema per voi semplicemente rimuovendo il punto e virgola dalla sua sintassi, aggiungendo il necessario dietro le quinte.

I commenti sono nello stesso formato dei commenti di Ruby, che inizia con un carattere hash:

	# Un commento

I commenti su più righe sono supportate e sono presenti anche dal JavaScript che viene generato. Sono racchiusi da tre caratteri di hash:

	###
	  Un commento su più righe , forse una LICENZA.
	###

Mentre leggi gli esempi del libro, può valere la pena copiare-incollare gli esempi qui presenti sul [compilatore online](http://jashkenas.github.com/coffee-script/ "Compilatore Online") e vedere quale codice JavaScript viene generato.

Come ho brevemente accennato, lo spazio  bianco è significativo in CoffeeScript. In pratica, ciò significa che è possibile sostituire le parentesi graffe (`{}`) con un tab. Questo prende ispirazione dalla sintassi di Python, e ha l'eccellente effetto collaterale di garantire che lo script sia formattato correttamente, altrimenti non compilerà nemmeno!

## Variabili e Scope ##

CoffeeScript risolve uno dei principali spauracchi presenti in JavaScript, le variabili globali. In JavaScript, è fin troppo semplice dichiarare accidentalmente una variabile globale dimenticando di
scrivere `var` prima della definizione della variabile. CoffeeScript risolve questo semplicemente rimuovendo
le variabili globali. Dietro le quinte, CoffeeScript avvolge lo script in una funzione anonima, mantenendo un contesto locale, e assegna automaticamente il prefisso `var` per tutte le assegnazioni di variabili. Per esempio, prendiamo questa semplice assegnazione di variabile in CoffeeScript:

	myVariable = "test"

Come puoi vedere, l'assegnazione della variabile viene mantenuta completamente locale, è impossibile creare accidentalmente una variabile globale. CoffeeScript fa effettivamente un passo ulteriore, e
rende difficile dichiarare variabili di livello superiore. Questo evita il più comune errore degli sviluppatori in JavaScript.

Tuttavia, a volte è utile per creare variabili globali. È possibile far ciò 
impostandole direttamente come proprietà dell'oggetto globale (`window` nei browser), o seguendo il seguente pattern:

	exports = this
	exports.MyVariable = "foo-bar"

Nel contesto del codice CoffeeScript, `this` è uguale all'oggetto globale, e creando una  variabile locale `exports` si sta rendendo davvero evidente a chiunque legga esattamente il codice quali variabili globali sta creando lo script. Inoltre, spiana la strada ai moduli CommonJS, che andremo a coprire più avanti nel libro.

## Funzioni ##

CoffeeScript rimuove l'istruzione `function` piuttosto verbosa, e la sostituisce con un una sottile freccia: `->`. Le funzioni possono essere one-liner o sparse su più righe. L' ultima espressione nella funzione viene restituita implicitamente. In altre parole, non è necessario utilizzare l'espressione `return` se non vuoi restituire prima all'interno della funzione.

Sapendo ciò, diamo un'occhiata a un esempio:

	func = -> "bar"

Si può vedere nella risultante compilazione che `->` è trasformato in una espressione `funzione` e la stringa `"bar"` viene restituita automaticamente.

Come accennato in precedenza, non c'è alcun motivo per cui non usare più righe, fintanto che tutto il contenuto rientri nel corpo della funzione correttamente:

	func = ->
	  # Una linea supplementare
	  "bar"

### Argomenti della funzione ###

Che ne dici di specificare gli argomenti? Beh, CoffeeScript permette di farlo specificando gli argomenti tra parentesi prima della freccia:

	moltiplicazione = (a, b) -> a * b

CoffeeScript supporta anche degli argomenti predefiniti. Per esempio:

	moltiplicazione = (a = 1, b = 2) -> a * b

È inoltre possibile utilizzare simboli per accettare più argomenti (indicati con...):

	sum = (nums...) ->
	  risultato = 0
	  nums.forEach (n) -> risultato += n
	  risultato

Nell'esempio precedente, `nums` è un array di tutti gli argomenti passati alla funzione. Non è l'oggetto `arguments`, ma piuttosto un vero e proprio array, quindi non c'è bisogno di preoccuparvi con `Array.prototype.splice` o `jQuery.makeArray()` se lo si vuole manipolare.

	trigger = (eventi...) ->
	  eventi.splice(1, 0, this)
  	  this.constructor.trigger.apply(eventi)

### Chiamare una funzione ###

Le funzioni possono essere invocate  sattamente come in JavaScript, con parentesi `()`, `apply()` , o `call()`.

Tuttavia, come in Ruby, CoffeeScript chiama automaticamente la funzione se vengono invocate con almeno un argomento:

	a = "Howdy!"

	alert a
	# Equivalente a:
	alert(a)

	alert inspect a
	# Equivalente a:
	alert(inspect(a))


Sebbene le parentesi siano facoltative, io consiglierei di usarle quando non è immediatamente ovvio ciò che è viene richiamato, e con quali argomenti. Nell'ultimo esempio, con `inspect`, io consiglio vivamente di mettere almeno l'invocazione `inspect` fra parentesi:

	alert inspect(a)

Se non si passano argomenti con una invocazione, CoffeeScript non ha modo di capire se si ha intenzione di richiamare la funzione o semplicemente di trattarla come una variabile. A tale riguardo, il comportamento di CoffeeScript differisce da Ruby, che richiama sempre riferimenti a funzioni, ed è più simile a Python. Questa è stata la fonte di alcuni errori nei miei
programmi CoffeeScript, quindi vale la pena tener d'occhio i casi in cui si intende
chiamare una funzione senza argomenti, e includere parentesi.

### Contesto della funzione ###

I cambiamenti di contesto sono all'ordine del giorno all'interno JavaScript, soprattutto con le callback degli eventi, così CoffeeScript fornisce alcuni aiuti per gestire il tutto. Uno di questi è una variante di `->`, La funzione freccia grassa: `=>`

Utilizzando la freccia grassa invece della freccia sottile ci si assicura che il contesto della funzione sarà vincolato a quello locale. Per esempio:

	this.clickHandler = -> alert "cliccato"
	element.addEventListener "click", (e) => this.clickHandler(e)

La ragione per cui si potrebbe voler fare ciò è che le callback da `addEventListener()` sono eseguite nel contesto di `element`, in questo caso `this` equivale all'elemento. Se vuoi mantenere `this` uguale al contesto locale, senza fare il solito `self = this`, le frecce grasse sono la strada da percorrere.

Questa idea associazione è un concetto simile a quello del [proxy()](http://api.jquery.com/jQuery.proxy/ "proxy()") di jQuery o alla funzione [bind()](https://developer.mozilla.org/en/JavaScript/Reference/Global_Objects/Function/bind "bind()") dell'ES5.

## Oggetti letterali e definizione di Array ##

Gli oggetti letterali possono essere specificati esattamente come in JavaScript, con un paio di parentesi graffe e
dichiarazioni chiave/valore. Tuttavia, come con la chiamata a funzione, CoffeeScript rende le parentesi opzionali. In realtà, è possibile utilizzare l'indentazione e nuove linee, invece di virgola di separazione:

	oggetto1 = {uno: 1, due: 2}
	# Senza parentesi
	oggetto2 = uno: 1, due: 2
	# Usando nuove linee invece di virgole
	oggetto3 = 
	  uno: 1
	  due: 2

	User.create(nome: "John Smith")


Allo stesso modo, gli array possono utilizzare lo spazio bianco invece di separatori virgola, anche se le parentesi quadre (`[]`) sono ancora necessarie:

	array1 = [ 1 , 2 , 3 ]
	array2 = [
	  1
	  2
	  3
	]
	array3 = [ 1,2,3 , ]

Come si può vedere in questo esempio, CoffeeScript ha anche lasciato una virgola finale in `array3`, un'altra fonte comune di errori di cross-browser.

## Controllo del flusso ##

La convenzione di parentesi opzionali  continua con le istruzioni CoffeeScript `if` e `else`:

	if true == true
	  "Siamo "ok"
	if true != true then "Panico"
	# Equivalente a:
	#  (1 > 0) ? "Ok" : "Y2K!"
	if 1 > 0 then "Ok" else "Y2K!"

Come puoi vedere sopra, se l'istruzione `if` è su una riga, è necessario utilizzare la keyword `then` in modo che CoffeeScript sappia quando inizia il blocco. Gli operatori condizionali (`?:`) non sono supportati, meglio utilizzare una sola riga di istruzione `if/else`.

CoffeeScript include anche un idioma Ruby che permette le dichiarazioni suffisse di `if`:

	alert "C'è freddo!" if temperatura < 5

Invece di utilizzare il punto esclamativo (`!`) per la negazione, è possibile utilizzare anche la keyword `!` che a volte rende il codice più leggibile, essendo i punti esclamativi può semplici da perdere:

	if not true then "Panico"

Nell'esempio precedente, potremmo anche usare meno la dichiarazione `unless` CoffeeScript, l'opposto di `if`:

	unless true
	  "Panico"

In un modo simile a `not`, CoffeeScript introduce anche la dichiarazione `is`, che
si traduce nei `===` di JavaScript:

se vero è 1
  " Coercizione di tipo sicuro ! "
In alternativa è non , è possibile utilizzare per non è :

if true is 1
  "Conversione del tipo fallita!"

Avrai notato che in questi esempi CoffeeScript sta convertendo gli operatori `==` in `===` e `!=` in `!==`. Questa è una delle mie funzioni preferite del linguaggio, e tuttavia una delle più banali. Qual è il ragionamento dietro tutto questo? Beh, francamente, la conversione del tipo in JavaScript è un po' strana, e il suo operatore di uguaglianza può portare ad una conversione non voluta del tipo, al fine di confrontarli, portando quindi a comportamenti non aspettati e a fonte di molti bug. C'è un lunga discussione su questo argomento nel Capitolo 6.

## Interpolazione di Stringhe ##

CoffeeScript porta lo stile dell'interpolazione di stringhe di Ruby in JavaScript. Stringhe fra doppie virgolette
possono contenere tag `#{}`, che contengono espressioni da interpolare nella stringa:
	
	colore_preferito = "Blu. No, gial..."
	domanda = "Bridgekeeper: Quale... è il tuo colore preferito?
	            Galahad: #{favorite_color}
	            Bridgekeeper: Sbagliato!
	            "

Come si può vedere in questo esempio, sono supportate le stringhe su più linee, senza dover aggiungere alla fine di ogni riga il tipico `+`.

## Cicli e Comprehensions ##

L'iterazione di Array in JavaScript ha una sintassi piuttosto arcaica, che ricorda un vecchio linguaggio come C piuttosto che un moderno linguaggio orientato ad oggetti. L'introduzione di ES5 ha migliorato molto tale situazione, con la funzione `forEach()`, ma che richiede ancora una chiamata a funzione per ogni iterazione ed è quindi molto lenta. Anche in questo caso, viene CoffeeScript in nostro soccorso, con una bella sintassi:

	for nome in ["Roger", "Roderick", "Brian"]
	  alert "Rilasciare #{nome}"

Se è necessario l'indice iterazione corrente, basta passare un argomento in più:

	for nome, i in ["Roger il borseggiatore", "Roderick il rapinatore"]
	  alert "#{i} - Rilasciare #{name}"

È anche possibile iterare su una sola riga, utilizzando la forma postposta:

	rilasciare prigioniero for prigionieri in ["Roger", "Roderick", "Brian"]

Come nella Comprehension di Python, è possibile filtrare:

	prigionieri = ["Roger" , "Roderick", "Brian"]
	rilascia prigioniero for prigionierio in prigionieri when prigioniero[0] is "R"

È inoltre possibile utilizzare la comprehension per iterare su proprietà presenti all'interno di oggetti. Invece della keywork `in`, usa `of`:

	nomi = sam: seaborn, donna: moss
	alert("#{primo} #{ultimo}") for primo, ultimo of nomi

L'unico ciclo di basso livello che CoffeeScript mantiene è il ciclo `while`. Questo ha un comportamento simile del `while` in puro JavaScript, ma ha il vantaggio di restituire un array di risultati (come la funzione `Array.prototype.map()`):

	num = 6
	menestrello = while num -= 1
	  num + " Il coraggioso Sir Robin è scappato"

## Array ##

CoffeeScript prende ispirazione da Ruby quando si tratta di divisione di array in intervalli. Gli intervalli sono creati da due valori numerici, la prima e l'ultima posizione nell'intervallo, separati da `..` o `...`. Se un intervallo non è preceduto da nulla, CoffeeScript lo rende un array:

	intervallo = [1..5]

Se, tuttavia, l'intervallo è specificato subito dopo una variabile, CoffeeScript lo converte in una chiamata a `slice()`:

	primoDue = ["uno", "due", "tre"][0..1]

Nell'esempio precedente, l'intervallo restituisce un nuovo array, contenente solo i primi due elementi dell'array originale. È inoltre possibile utilizzare la stessa sintassi per la sostituzione di un segmento dell'array con un altro array:

	numeri = [0..9]
	numeri[3..5] = [-3, -4, -5]

La cosa bella, è che JavaScript permette di chiamare `slice()` anche su stringhe, in modo da poter utilizzare intervallo con stringhe per restituire un nuovo sottoinsieme di caratteri:

	mia = "mia stringa" [0..1]

Per controllare se esiste un valore all'interno di un array è sempre una noia in JavaScript, poiché `indexOf()` non ha ancora (al tempo di pubblicazione del libro *17 Gennaio 2012*) pieno supporto cross-browser (Internet Explorer, io sto parlando di te). CoffeeScript risolve questo problema con l'operatore `in`, ad esempio:

parole = ["rattled", "roudy", "rebbles", "ranks"]
alert "Stop wagging me" if "ranks" in words

## Alias ​​e l'Operatore Esistenziale ##

CoffeeScript include alcuni alias utili per risparmiare del tempo. Uno di questi alias è `@`, che può essere usato al posto di `this`:

	@saviour = true

Un altro è `::`, che è un alias per `prototype`:

	User::first = -> @records[0]

Utilizzare l`if` per verificare che una variabile sia `null` è comune in JavaScript, ma ha un paio di problematiche in quanto le stringhe sono vuote o lo zero vengono convertiti in `false`, che può essere un risultato non aspettato. L'operatore esistenziale `?` di CoffeeScript restituisce `true` a meno che una variabile sia `null` o `undefined`, simile al `nil?` di Ruby:

	lode if brian?

È inoltre possibile utilizzarlo al posto del l'operatore `||`:

	velocità = meridionale ? 40

Se devi fare un controllo `null` prima di accedere ad una proprietà, è possibile evitarlo ponendo
l'operatore esistenziale a proprio prima di esso. Questo è simile al metodo [try](http://guides.rubyonrails.org/active_support_core_extensions.html#try "Metodo try") di Active Support:

	blackKnight.getLegs()?.kick()

Allo stesso modo, è possibile verificare che una proprietà è in realtà una funzione, ed invocabile, ponendo l'operatore esistenziale a destra prima delle parentesi. Se la proprietà non esiste, o non è un
funzione, semplicemente non viene chiamata:

	blackKnight.getLegs().kick?()